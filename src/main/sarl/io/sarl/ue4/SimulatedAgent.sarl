/** 
 * 
 */
package io.sarl.ue4

import io.sarl.core.Initialize
import io.sarl.ue4.events.SimulationStep
import io.sarl.ue4.influence.SendInfluence
import io.sarl.ue4.influence.UdpSendInfluence
import io.sarl.ue4.perception.PerceptionsReceived
import io.sarl.core.Logging

/** 
 * Agent able to send influences on the world
 * @author Alexandre Lombard
 * 
 */
agent SimulatedAgent {
	uses Logging, SendInfluence

	val initialScale = 100.0
	var direction = new Vector3D(initialScale * Math.random(), initialScale * Math.random(), initialScale * Math.random())

	on Initialize {
		setSkill(new UdpSendInfluence, SendInfluence)
	}
	
	on SimulationStep {
		synchronized (direction) {
			// Slightly update the direction
			//direction = direction + new Vector3D(Math.random() * 2 - 1, Math.random() * 2 - 1, Math.random() * 2 - 1)
		
			// Send the influence
			sendInfluence(direction)
		}
	}
	
	on PerceptionsReceived {
		if (occurrence.perceptionList.perceptions.size > 0) {
			var to = new Vector3D(0.0, 0.0, 0.0)

			// Average position
			for (v : occurrence.perceptionList.perceptions)
				to = to + v
			to = to * (1.0 / occurrence.perceptionList.perceptions.size)
			
			to = to / to.length // Normalize
			
			to = to * -1.0		// Invert
			to = to * 10000.0		// Scale

			synchronized (this) {
				this.direction = to
			}

			info("New direction: " + to)	
		}
	}
}
 