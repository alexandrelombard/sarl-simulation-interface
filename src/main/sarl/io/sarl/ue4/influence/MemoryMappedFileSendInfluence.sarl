/** 
 * 
 */
package io.sarl.ue4.influence

import io.sarl.ue4.Vector3D
import java.io.File
import java.nio.MappedByteBuffer
import java.nio.channels.FileChannel
import java.nio.channels.FileChannel.MapMode
import java.nio.file.StandardOpenOption

/** 
 * Allows sending influences by pushing them in a shared memory segment (a memory mapped file).
 * @author Alexandre Lombard
 * 
 */
skill MemoryMappedFileSendInfluence implements SendInfluence {

	/** The name of the memory mapped file (currently hard-coded) */
	val fileName = "SHM_SARL_UE4"

	var channel: FileChannel = null
	var mappedByteBuffer: MappedByteBuffer = null 

	def install {
		synchronized (this) {
			channel = FileChannel.open(
				new File(fileName).toPath,
				StandardOpenOption.READ,
				StandardOpenOption.WRITE,
				StandardOpenOption.CREATE)
			mappedByteBuffer = channel.map(MapMode.READ_WRITE, 0, 4096)		
		}
	}

	def uninstall {
		synchronized (this) {
			channel.close
		}
	}
	
	def sendInfluence(force : Vector3D) {
		// Serialization
		val influence = new Influence(force, ID)
		val buffer = influence.toByteArray
		
		// Sending packet
		synchronized (this) {
			mappedByteBuffer.put(buffer)
		}
	}
	
}
